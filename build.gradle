buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5'
        classpath "com.avast.gradle:docker-compose-gradle-plugin:0.3.3"
    }
}

plugins {
    id 'net.researchgate.release' version '2.4.0'
}

group 'cz.alenkacz.gradle'

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'docker-compose'

repositories {
    jcenter()
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

dockerCompose.isRequiredBy(test)
dockerCompose {
    useComposeFiles = ['docker-compose-test.yml']
}

test.doFirst {
    dockerCompose.exposeAsEnvironment(test)
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.12"
}

dependencies {
    compile gradleApi()

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile gradleTestKit()
    testCompile ('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group:'org.codehaus.groovy'
    }
    testCompile 'com.github.mohitsoni:marathon-client:0.4.2'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'gradle-marathon-deployer'
        desc = 'Gradle plugin that will deploy your application to the Marathon cloud.'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/alenkacz/gradle-marathon-deployer.git'
        websiteUrl = 'https://github.com/alenkacz/gradle-marathon-deployer'
        issueTrackerUrl = 'https://github.com/alenkacz/gradle-marathon-deployer/issues'
        labels = ['marathon', 'mesos', 'docker', 'deploy', 'cloud']
        version {
            name = project.version
            vcsTag = project.version
        }
    }
}

afterReleaseBuild.dependsOn bintrayUpload